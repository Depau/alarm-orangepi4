# Maintainer: Davide Depau <davide@depau.eu>

pkgname=linux-orangepi4-dtbs
pkgver=5.8.0
_srcname=linux-5.8
_armbianrev=36405e3397b68600bdd2c34d5a7bcfa0a09b1226
pkgrel=1
pkgdesc="Orange Pi 4 DTBs from Armbian"
arch=('aarch64')
url='https://github.com/armbian/build/tree/master/patch/kernel/rockchip64-current'
license=('GPL2')
depends=('linux-aarch64')
makedepends=('git' 'dtc')
source=(
        "http://www.kernel.org/pub/linux/kernel/v5.x/${_srcname}.tar.xz"
        "config"  # from ALARM linux-aarch64
        "armbian-patches::git+https://github.com/armbian/build.git#commit=${_armbianrev}"
        "0001-arm64-dts-Fix-rk3399-orangepi-4-dts-symbol-usage.patch"
)

_patches=(
        "RK3399-enable_1.5_2.0_ghz_cpufreq_opp.patch"
        "add-board-orangepi-4.patch"
        "rk3399-add-sclk-i2sout-src-clock.patch"
        "rk3399-sd-drive-level-8ma.patch"
        "unlock-temperature.patch"
)

prepare() {
        cd "${_srcname}"

        for patch in "${_patches[@]}"; do
                msg2 "Applying $patch"
                patch -Np1 < "${srcdir}/armbian-patches/patch/kernel/rockchip64-current/${patch}"
        done

        msg2 "Applying 0001-arm64-dts-Fix-rk3399-orangepi-4-dts-symbol-usage.patch"
        patch -Np1 < "${srcdir}/0001-arm64-dts-Fix-rk3399-orangepi-4-dts-symbol-usage.patch"

        cat "${srcdir}/config" > ./.config
        make olddefconfig
}

build() {
        cd "${_srcname}"

        make ${MAKEFLAGS} DTC_FLAGS="-@" dtbs 
}

package() {
        cd "${_srcname}"

        install -Dm644 "arch/arm64/boot/dts/rockchip/rk3399-orangepi-4.dtb" "${pkgdir}/boot/dtbs/rockchip/rk3399-orangepi-4.dtb"
}
md5sums=('0e5c4c15266218ef26c50fac0016095b'
         'cc2ae6ac43ab9e6170bfed62f73e4968'
         'SKIP'
         '67e1998c82ab2d06a78f8eb57cb69132')
