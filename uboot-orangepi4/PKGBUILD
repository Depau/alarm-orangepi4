# Maintainer: Davide Depau <davide@depau.eu>
# Contributor: Nadia Holmquist Pedersen <nadia@nhp.sh>

pkgname=uboot-orangepi4
pkgver=2020.07
_ubootver=$(echo "$pkgver" | sed "s/rc/-rc/")
_tfatag=v2.3
_armbianrev=36405e3397b68600bdd2c34d5a7bcfa0a09b1226
pkgrel=2
pkgdesc='U-boot for Orange Pi 4'
arch=('aarch64')
url='https://www.denx.de/wiki/U-Boot'
license=('GPL2')
install=${pkgname}.install
makedepends=('bc' 'make' 'gcc' 'dtc' 'uboot-tools' 'arm-none-eabi-gcc' 'python')

source=(
	"git+https://github.com/ARM-software/arm-trusted-firmware.git#tag=${_tfatag}"
	"ftp://ftp.denx.de/pub/u-boot/u-boot-${_ubootver}.tar.bz2"
	"armbian-patches::git+https://github.com/armbian/build.git#commit=${_armbianrev}"
)
sha256sums=('SKIP'
            'c1f5bf9ee6bb6e648edbf19ce2ca9452f614b08a9f886f1a566aa42e8cf05f6a'
	    'SKIP')

prepare() {
	cd "${srcdir}/u-boot-${_ubootver}"

	# Apply Armbian patches for Orange Pi 4 support
	find "${srcdir}/armbian-patches/patch/u-boot/u-boot-rockchip64-mainline" -type f -name '*.patch' | while read -r patch; do
	  patch -Np1 -i "$patch"
	done
}

build() {
	unset CFLAGS CXXFLAGS CPPFLAGS LDFLAGS

	make -C "${srcdir}"/arm-trusted-firmware PLAT=rk3399
	make -C "${srcdir}"/u-boot-${_ubootver} orangepi-4-rk3399_defconfig
	make -C "${srcdir}"/u-boot-${_ubootver} BL31="${srcdir}"/arm-trusted-firmware/build/rk3399/release/bl31/bl31.elf
}

package() {
	cd "${srcdir}/u-boot-${_ubootver}"

	install -Dm 644 idbloader.img "${pkgdir}"/boot/idbloader.img
	install -Dm 644 u-boot.itb "${pkgdir}"/boot/u-boot.itb
}
