# Maintainer: Davide Depau <davide@depau.eu>
# Contributor: Nadia Holmquist Pedersen <nadia@nhp.sh>

pkgname=uboot-orangepi4
pkgver=2020.10
_ubootver=$(echo "$pkgver" | sed "s/rc/-rc/")
_tfatag=v2.4
pkgrel=1
pkgdesc='U-boot for Orange Pi 4'
arch=('aarch64')
url='https://www.denx.de/wiki/U-Boot'
license=('GPL2')
install=${pkgname}.install
makedepends=('bc' 'make' 'gcc' 'dtc' 'uboot-tools' 'arm-none-eabi-gcc' 'python')

source=(
	"git+https://github.com/ARM-software/arm-trusted-firmware.git#tag=${_tfatag}"
	"ftp://ftp.denx.de/pub/u-boot/u-boot-${_ubootver}.tar.bz2"
	"0001-Add-board-Orange-Pi-4.patch"
)
sha256sums=('SKIP'
            '0d481bbdc05c0ee74908ec2f56a6daa53166cc6a78a0e4fac2ac5d025770a622'
            '667d9b35e2b038a11d6d88df065b9f75a0af84f34210c0e04377094d9433664b')

prepare() {
	msg "Patching u-boot"
	cd "${srcdir}/u-boot-${_ubootver}"
  
	patch -Np1 < "${srcdir}/0001-Add-board-Orange-Pi-4.patch"
}

build() {
	unset CFLAGS CXXFLAGS CPPFLAGS LDFLAGS

	make -C "${srcdir}"/arm-trusted-firmware PLAT=rk3399
	make -C "${srcdir}"/u-boot-${_ubootver} orangepi-4-rk3399_defconfig
	make -C "${srcdir}"/u-boot-${_ubootver} BL31="${srcdir}"/arm-trusted-firmware/build/rk3399/release/bl31/bl31.elf
}

package() {
	cd "${srcdir}/u-boot-${_ubootver}"

	install -Dm 644 idbloader.img "${pkgdir}"/boot/idbloader.img
	install -Dm 644 u-boot.itb "${pkgdir}"/boot/u-boot.itb
}
